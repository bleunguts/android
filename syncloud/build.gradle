apply plugin: 'com.android.application'

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 14
        versionCode 134
        versionName "1.34"
    }

    productFlavors {
        beta {
            applicationId "org.syncloud.android.beta"
        }

        prod {
            applicationId "org.syncloud.android"
        }
    }

    signingConfigs {
        release {
            storeFile file('syncloud.keystore')
            storePassword ''
            keyAlias ''
            keyPassword ''
        }
    }

    buildTypes {
        release {
            debuggable false
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'log4j.properties'
        exclude 'about.html'
        exclude 'META-INF/beans.xml'
    }

    allprojects {
        afterEvaluate { project ->

            def props = new Properties()
            def keystoreProperies = System.properties['user.home'] + '/keystore.properties'
            def keyProps = rootProject.file(keystoreProperies)
            if (keyProps.exists()) {
                props.load(new FileInputStream(keyProps))
                android.signingConfigs['release'].storeFile = file(props['storeFile'])
                android.signingConfigs['release'].storePassword = props['storePassword']
                android.signingConfigs['release'].keyAlias = props['keyAlias']
                android.signingConfigs['release'].keyPassword = props['keyPassword']
            } else {
                logger.warn(keystoreProperies + ' is not available, will not be able to publish to Google Play')
            }
        }
    }

}


dependencies {
    compile project(':platform')
    compile project(':apps')
    compile project(':redirect')
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'javax.jmdns:jmdns:3.4.1'
    compile 'org.bitlet:weupnp:0.1.2'
    compile 'org.apache.httpcomponents:httpclient-android:4.3.3'
    compile 'com.android.support:support-v4:20.0.0'
    compile 'com.android.support:appcompat-v7:20.0.0'
    compile 'de.mindpipe.android:android-logging-log4j:1.0.3'
    compile 'ch.acra:acra:4.5.0'

}
